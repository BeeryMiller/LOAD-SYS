VDPWD  EQU  >F100
VDPWA9 EQU  >F102
INTKB  EQU  8
CLRKB  EQU  9
CBH92  BYTE >92
CBH89  BYTE >89
CBH3F  BYTE >3F
*
SNDOFS BYTE >9F,>BF,>DF,>FF   CONSTANTS TO TURN OFF SOUND
*
       BYTE >64          needed for even; =add=
       EVEN
*===================================*
*                                   *
* character set for errors          *
*                                   *
*===================================*
CHRSLH DATA >0002,>0408,>1020,>4000 "/"
*
CHRTBL
CHR65  DATA >0038,>447C,>4444,>4400 "A"
CHR66  DATA >0078,>4478,>4444,>7800
CHR67  DATA >0038,>4440,>4044,>3800
CHR68  DATA >0070,>4844,>4448,>7000
CHR69  DATA >007C,>4078,>4040,>7C00
CHR70  DATA >007C,>4078,>4040,>4000
CHR71  DATA >0038,>4440,>4C44,>3C00
CHR72  DATA >0044,>447C,>4444,>4400
CHR73  DATA >0038,>1010,>1010,>3800
CHR74  DATA >001C,>0808,>0848,>3000
CHR75  DATA >0048,>5060,>5048,>4400
CHR76  DATA >0040,>4040,>4040,>7C00
CHR77  DATA >0044,>6C54,>5444,>4400
CHR78  DATA >0044,>6464,>544C,>4400
CHR79  DATA >0038,>4444,>4444,>3800
CHR80  DATA >0078,>4444,>7840,>4000
CHR81  DATA >0038,>4444,>5448,>3400
CHR82  DATA >0078,>4444,>7848,>4400
CHR83  DATA >003C,>4038,>0404,>7800
CHR84  DATA >007C,>1010,>1010,>1000
CHR85  DATA >0044,>4444,>4444,>3800
CHR86  DATA >0044,>4428,>2810,>1000
CHR87  DATA >0044,>4454,>5454,>2800
CHR88  DATA >0044,>2810,>2844,>4400
CHR89  DATA >0044,>2810,>1010,>1000
CHR90  DATA >007C,>0408,>1020,>7C00  "Z"
ENDCTB EQU  $
* NOW THIS ROUTINE WILL DISPLAY ON THE SCREEN THE ERROR MESSAGE POINTED
*  TO BY R0      IF AN ERROR IS DETECTED, THE COMPUTER WILL
*     PERFORM A COLD RESTART IF THE USER WANTS TO TRY TO REBOOT AGAIN
*
*   SELECT THE UPPER 64K OF RAM TO PUT MESSAGE IN
*
BADLOD LI   R2,>F120
       MOVB @CBH89,*R2          NOW SOUND AN ERROR
       MOVB @CBH3F,*R2
       MOVB @CBH92,*R2
       LI   R2,VRGTBL
NEXREG MOV  *R2+,R1
       JEQ  NEXRE1            ALL DONE SO TEST FOR KEYBOARD
       BL   @SETVRG
       JMP  NEXREG

* NOW USE TEXT 1 MODE TO DISPLAY THE ERROR MESSAGE ON THE SCREEN
* ASSUME THAT THE PATTERN NAME TABLE IS AT >0000
* AND THAT THE PATTERN GENERATOR TABLE IS AT >0800
* FIRST MOVE IN 24*40 LINES OF BLANK
NEXRE1 LI   R1,>0040          SET ADDRESS OF WRITE
       BL   @SETVRG
       LI   R2,24*40          40*24 SCREEN
       LI   R1,>2020          PUT IN BLANKS
BLKLOP MOVB R1,@VDPWD
       DEC  R2
       JNE  BLKLOP
* NOW PUT IN THE ERROR STATEMENT
       LI   R1,>E041          PUT ON LINE 13
       BL   @PUTMSG
* NOW LOAD IN PRESS ANY KEY TO RETRY
       LI   R0,ASK1
       LI   R1,>3042
       BL   @PUTMSG
* NOW LOAD IN CHARACTER SET
       LI   R1,>0048
       BL   @SETVRG
       CLR  R1
       LI   R2,>41*8          BLANK FOR 0 TO >40
BPTLOP MOVB R1,@VDPWD
       DEC  R2
       JNE  BPTLOP
       LI   R1,CHRTBL
       LI   R2,ENDCTB-CHRTBL
CTBLOP MOVB *R1+,@VDPWD
       DEC  R2
       JNE  CTBLOP
       LI   R1,>7849     LOACTION FOR "/"
       BL   @SETVRG
       LI   R1,CHRSLH
       LI   R2,8
CTBSLH MOVB *R1+,@VDPWD
       DEC  R2
       JNE  CTBSLH
       CLR  R2
CTBLO1 DEC  R2
       JNE  CTBLO1
       BL   @SNDOFF           NOW TURN OFF SOUND
* NOW JUST TEST FOR A RETRY FROM THE KEYBOARD

       LI   R12,>1EE0    *add*-=-=-=-
       SBZ  CLRKB     >09          *add*-=-=-=-    keyboard
       SBO  CLRKB     >09          *add*-=-=-=-=

WAITKY CLR  R12
       TB   INTKB
       JEQ  WAITKY
*r     SBZ  CLRKB             CLEAR KEYBOARD INPUT
*r     SBO  CLRKB
       BLWP @0                TRY TO REBOOT
*
SETVRG MOVB R1,@VDPWA9
       SRC  R1,8
       MOVB R1,@VDPWA9
       SRC  R1,8
       RT
*
PUTMSG MOV  R11,R10
       BL   @SETVRG
       MOVB *R0+,R2           GET LENGTH OF ERROR MESSAGE
       SRL  R2,8
EMSLOP MOVB *R0+,@VDPWD
       DEC  R2
       JNE  EMSLOP
       B    *R10
*
SNDOFF LI   R1,SNDOFS
SNDOF1 MOVB *R1+,@>F120
       CI   R1,SNDOFS+4
       JNE  SNDOF1
       RT
*
VRGTBL DATA >0080,>5081,>4082,>2184,>F787,>0888,>0089,>048E
CBH0   DATA 0
SLAST

MYBUF  BSS  256      *nothing saved past SLAST!
MYBUF1 BSS  256
FLPTAB BSS  256
*      eof
